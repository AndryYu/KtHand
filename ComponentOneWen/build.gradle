// 1、取出isComponent字段
def isComponent = rootProject.ext.isComponent

// 2、根据isComponent字段，来确定当前是集成化 还是组件化
if (isComponent) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'

def version = rootProject.ext.AndroidID
def appId = rootProject.ext.AppID

// 调用清单文件处理函数
rootProject.ext.manifestRelease(project.name)
android {
    compileSdkVersion version.compileSdkVersion
    buildToolsVersion version.buildToolsVersion

    defaultConfig {
        minSdkVersion version.minSdkVersion
        targetSdkVersion version.targetSdkVersion
        if (isComponent){
            applicationId appId.oneWen
        }
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    signingConfigs {
        //发布版本
        release {
            storeFile file(rootProject.ext.signing["release-store-file"])
            storePassword rootProject.ext.signing["release-storePassword"]
            keyAlias rootProject.ext.signing["release-keyAlias"]
            keyPassword rootProject.ext.signing["release-keyPassword"]
        }
        //开发版本配置
        debug {
            storeFile file(rootProject.ext.signing["release-store-file"])
            storePassword rootProject.ext.signing["release-storePassword"]
            keyAlias rootProject.ext.signing["release-keyAlias"]
            keyPassword rootProject.ext.signing["release-keyPassword"]
        }
    }

    // 维度
    flavorDimensions "env"
    productFlavors {
        product {
            dimension "env"
            signingConfig signingConfigs.release
            buildConfigField "boolean", "PAY_MODE", "false"
            //服务器商用url
            buildConfigField "String", "BASE_URL", '"https://39.96.43.68:9001/IE_EM_APPP/"'
            buildConfigField "String", "WXFF_BASE_URL", '"https://www.sumapay.com/aggregatePayMobile/merchantApp.do"'
        }
        dev {
            dimension "env"
            signingConfig signingConfigs.release
            buildConfigField "boolean", "PAY_MODE", "false"
            //服务器测试url
            buildConfigField "String", "BASE_URL", '"http://1.119.150.199:21883/IE_EM_APKP/"'
            buildConfigField "String", "WXFF_BASE_URL", '"https://www.sumapay.com/aggregatePayMobile/merchantApp.do"'
        }
    }

    sourceSets{
        main{
            if (isComponent){
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }else{
                manifest.srcFile 'src/main/_ReleaseManifest/AndroidManifest.xml'
                java{
                    //屏蔽测试代码
                    exclude "**/debug/**"
                }
            }
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    api project(path: ':common')
}